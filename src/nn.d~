
/************************************************************************

Module: nn.c (approximate nearest-neighbor matching)
Author: David Lowe (2006)

nn.c:
  This module finds the nearest-neighbors of vectors in high dimensional
      spaces using a search of multiple randomized k-d trees.

  The following routines are the interface to this module:

  Default constructor: this(ubyte **vecs, int vcount, int veclen, int numTrees)
    This routine creates an Index data structure containing the k-d trees
    and other information used to find neighbors to the given set of vectors.
        vecs: array of pointers to the vectors to be indexed. 
        vcount: number of vectors in vecs.
        veclen: the length of each vector.
        numTrees: the number of randomized trees to build.

  Destructor ~this()
    Frees all memory for the given index.

  FindNeighbors(int *result, int numNN, float *vec, int maxCheck)
  Find the numNN nearest neighbors to vec and store their indices in the
  "result" vector, which must have length numNN.  The returned indices
  refer to position in the original vecs used to create the index.
  Seach a maximum of maxCheck tree nodes for the result (this is what
  determines the amount of computation).


 *************************************************************************/

import std.c.stdlib;
import std.c.stdio;
import std.c.math;
import std.c.string;

import util;
import heap;



/* Contains the k-d trees and other information for indexing a set of points
   for nearest-neighbor matching.
 */

class KDTree {

	int numTrees;       /* Number of randomized trees that are used. */
	int checkCount;     /* Number of neighbors checked so far in this lookup. */
	float searchDistSq; /* Distance cutoff for searching (not used yet). */
	int vcount;         /* Number of vectors stored in this index. */
	int veclen;         /* Length of each vector. */
	int vsize; 			/* Space allocated for vector storage (vecs) and index (vind) */
	float **vecs;      /* Float vecs.  */
	int *vind;          /* Array of indices to vecs.  When doing
						   lookup, this is used instead to mark checkID. */
	
	int* freeInd;		/* Array of indices to free locations in vecs */
	int freeIndCount;	/* Count of elements in freeInd */
	int freeIndSize;	/* The size of freeInd */
	
	int checkID;        /* A unique ID for each lookup. */
	int ncount;         /* Number of neighbors so far in result. */
	float *dsqs;        /* Squared distances to current results. */
	int dsqlen;         /* Length of space allocated for dsqs. */
	TreeSt **trees;  /* Array of k-d trees used to find neighbors. */
	Heap!(BranchSt) heap;
	
	Pool pool;          /* Memory pool that holds all data for this index. */


	
	/*--------------------------- Constants -----------------------------*/
	
	/* When creating random trees, the dimension on which to subdivide is
		selected at random from among the top RandDim dimensions with the
		highest variance.  A value of 5 works well.
	*/
	const int RandDim=5;
	
	/* To improve efficiency, only SampleMean random values are used to
		compute the mean and variance at each level when building a tree.
		A value of 100 seems to perform as well as using all values.
	*/
	const int SampleMean = 100;
	
	
	/*--------------------- Internal Data Structures --------------------------*/
	
	/* This is a node of the binary k-d tree.  All nodes that have 
		vec[divfeat] < divval are placed in the child1 subtree, else child2.
		A leaf node is indicated if both children are NULL.
	*/
	struct TreeSt {
		int divfeat;    /* Index of the vector feature used for subdivision.
							If this is a leaf node (both children are NULL) then
							this holds vector index for this leaf. */
		float divval;   /* The value used for subdivision. */
		TreeSt* child1, child2;  /* Child nodes. */
	};
	alias TreeSt* Tree;
	
	/* This record represents a branch point when finding neighbors in
		the tree.  It contains a record of the minimum distance to the query
		point, as well as the node at which the search resumes.
	*/
	struct BranchSt {
		Tree node;           /* Tree node at which search resumes */
		float mindistsq;     /* Minimum distance to query for all nodes below. */
		
		int opCmp(BranchSt rhs) 
		{ 
			if (mindistsq < rhs.mindistsq) {
				return -1;
			} if (mindistsq > rhs.mindistsq) {
				return 1;
			} else {
				return 0;
			}
		}
		
		static BranchSt opCall(Tree aNode, float dist) 
		{
			BranchSt s;
			s.node = aNode;
			s.mindistsq = dist;
			
			return s;
		}
		
	}; 
	alias BranchSt* Branch;
	
	
	/*------------------------ Build k-d tree index ---------------------------*/
	
	/* Build and return the k-d tree index used to find nearest neighbors to
		a set of vectors. 
	vecs: array of pointers to the vectors to be indexed.
	vcount: number of vectors in vecs.
	veclen: the length of each vector.
	numTrees: the number of randomized trees to build.
	*/
	public this(float **vecs, int vcount, int veclen, int numTrees)
	{
		int i, j, rand;	
		pool = new Pool();    /* All data for the index goes into this pool. */
// 		index = pool.malloc!(IndexSt)();
// 		index.pool = pool;
		this.numTrees = numTrees;
		this.vcount = vcount;
		this.vsize = vcount;
		this.veclen = veclen;
		this.vecs = vecs;
		this.trees = pool.malloc!(Tree)(numTrees);
		this.heap = new Heap!(BranchSt)(512);
		this.dsqlen = 0;
		this.checkID = -1000;
		
		this.freeIndSize = 0;
		this.freeIndCount = 0;
	
		/* Create a permutable array of indices to the input vectors. */
		this.vind = pool.malloc!(int)(vcount);
		for (i = 0; i < vcount; i++)
			this.vind[i] = i;
	
		/* Construct the randomized trees. */
		for (i = 0; i < numTrees; i++) {
	
			/* Randomize the order of vectors to allow for unbiased sampling. */
			for (j = 0; j < vcount; j++) {
				rand = cast(int) (drand48() * vcount);  
				//rand = random(vcount)%vcount;  
				assert(rand >=0 && rand < vcount);
				swap(this.vind[j], this.vind[rand]);
			}
			this.trees[i] = null;
			DivideTree(& this.trees[i], 0, vcount - 1);
		}
	}
	
	
	/* Free all memory used to create this this.
	*/
	public ~this()
	{
		delete this.pool;
	}
	
	
	/* Create a tree node that subdivides the list of vecs from vind[first]
		to vind[last].  The routine is called recursively on each sublist.
		Place a pointer to this new tree node in the location pTree. 
	*/
	private void DivideTree(Tree* pTree, int first, int last)
	{
		// 	printf("First: %d, Last: %d\n",first, last);
		Tree node;
	
		//    node = NEW(TreeSt, this.pool);
		node = this.pool.malloc!(TreeSt)();
	
		*pTree = node;
	
		/* If only one exemplar remains, then make this a leaf node. */
		if (first == last) {
			node.child1 = node.child2 = null;    /* Mark as leaf node. */
			node.divfeat = this.vind[first];    /* Store index of this vec. */
		} else {
			ChooseDivision(node, first, last);
			Subdivide(node, first, last);
		}
	}
	
	
	/* Choose which feature to use in order to subdivide this set of vectors.
		Make a random choice among those with the highest variance, and use
		its variance as the threshold value.
	*/
	private void ChooseDivision(Tree node, int first, int last)
	{
		static float[] mean, var;	
		if (mean==null) {
			mean = new float[this.veclen];
			var = new float[this.veclen];
		}
		// Simpler D-specific initialization
		mean[] = 0.0;
		var[] = 0.0;
		/*
		for (i = 0; i < this.veclen; i++) {
			mean[i] = 0.0;
			var[i] = 0.0;
		}*/
		
		/* Compute mean values.  Only the first SampleMean values need to be
			sampled to get a good estimate.
		*/
		int end = MIN(first + SampleMean, last);
		int ind, count = 0;
		for (int j = first; j <= end; j++) {
			count++;
			ind = this.vind[j];
			for (int i = 0; i < this.veclen; i++)
				mean[i] += this.vecs[ind][i];
		}
		for (int i = 0; i < this.veclen; i++)
			mean[i] /= count;
	
		/* Compute variances (no need to divide by count). */
		for (int j = first; j <= end; j++) {
			ind = this.vind[j];
			for (int i = 0; i < this.veclen; i++) {
				float val = this.vecs[ind][i];
				float dist = val - mean[i];
				var[i] += dist * dist;
			}
		}
		/* Select one of the highest variance indices at random. */
		node.divfeat = SelectDiv(var);
		node.divval = mean[node.divfeat];
	}
	
	
	/* Select the top RandDim largest values from v and return the index of
		one of these selected at random.
	*/
	private int SelectDiv(float[] v)
	{
		int i, j, rand, num = 0;
		int topind[RandDim];
	
		/* Create a list of the indices of the top RandDim values. */
		for (i = 0; i < v.length; i++) {
			if (num < RandDim  ||  v[i] > v[topind[num-1]]) {
				/* Put this element at end of topind. */
				if (num < RandDim)
					topind[num++] = i;            /* Add to list. */
				else topind[num-1] = i;         /* Replace last element. */
				/* Bubble end value down to right location by repeated swapping. */
				j = num - 1;
				while (j > 0  &&  v[topind[j]] > v[topind[j-1]]) {				
					swap(topind[j], topind[j-1]);
					j--;
				}
			}
		}
		/* Select a random integer in range [0,num-1], and return that index. */
		//rand = random(num)%num;
		rand = cast(int) (drand48() * num);
		assert(rand >=0 && rand < num);
		return topind[rand];
	}
	
	
	/* Subdivide the list of exemplars using the feature and division
		value given in this node.  Call DivideTree recursively on each list.
	*/
	private void Subdivide(Tree node, int first, int last)
	{
		int i, j, ind;
		float val;
	
		/* Move vector indices for left subtree to front of list. */
		i = first;
		j = last;
		while (i <= j) {
			ind = this.vind[i];
			val = this.vecs[ind][node.divfeat];
			if (val < node.divval) {
				i++;
			} else {
				/* Move to end of list by swapping vind i and j. */
				swap(this.vind[i], this.vind[j]);
				j--;
			}
		}
		/* If either list is empty, it means we have hit the unlikely case
			in which all remaining features are identical.  We move one
			vector to the empty list to avoid need for special case.
		*/
		if (i == first)
			i++;
		if (i == last + 1)
			i--;
	
		DivideTree(& node.child1, first, i - 1);
		DivideTree(& node.child2, i, last);
	}
	
	
	
	/*----------------------- Insert and remove operations ------------------------*/
	
	
	private void IncreaseVecStorage()
	{
		int old_size = this.vsize;
		int new_size = old_size*2;
		
		int* old_vind = this.vind;
		float** old_vecs = this.vecs;
		
		//allocate new memory
		this.vind = this.pool.malloc!(int)(new_size);
		this.vecs = this.pool.malloc!(float*)(new_size);
		
		// copy old vector values
		for (int i=0;i<this.vcount;++i) {
			this.vind[i] = old_vind[i];
			this.vecs[i] = old_vecs[i];
		}
		
		this.vsize = new_size;
	}
	
	
	public void InsertElement(float *vec)
	{
		int ind = -1;
		
		//  Check first for "holes" in the array of vectors
		if (this.freeIndCount>0) {
			ind = this.freeInd[this.freeIndCount--];
		} else {
			if (this.vcount==this.vsize) {
				IncreaseVecStorage();
			}
			ind = this.vcount++;
		}
		// add the new vector to the array
		this.vecs[ind] = vec;
		this.vind[ind] = ind;
		
		// update all the trees
		for (int t = 0; t < this.numTrees; ++t ) {
			InsertIntoTree(this.trees[t],ind);
		}
	}
	
	private void InsertIntoTree(Tree node, int ind)
	{
		float* vec = this.vecs[ind];
		if (node.child1==null && node.child2==null) {
			// insert element
			
			node.child1 = this.pool.malloc!(TreeSt)();
			node.child2 = this.pool.malloc!(TreeSt)();
			
			node.child1.divfeat = ind;
			node.child2.divfeat = node.divfeat;
			
			// find dimension with greatest variance
			float var = -1;
			int feat = -1;
			for (int i=0; i<this.veclen; ++i ) {
				float tmp = ABS(vec[i]-this.vecs[node.divfeat][i]);
				if (tmp>var) {
					var = tmp;
					feat = i;
				}
			}
			
			if (vec[feat] > this.vecs[node.divfeat][feat]) {
				swap(node.child1, node.child2);
			}
			
			node.divfeat = feat;
			node.divval = (vec[feat] + this.vecs[node.divfeat][feat])/2;		
		} 
		else {
			if (vec[node.divfeat]< node.divval) {
				InsertIntoTree(node.child1, ind);
			} else {
				InsertIntoTree(node.child2, ind);
			}
		}
	}
	
	
	private void IncreaseFreeInd() 
	{
		int old_size = this.freeIndSize;
		int new_size = (old_size==0?8:old_size*2);
		
		int* old_freeInd = this.freeInd;
		
		//allocate new memory
		this.freeInd = this.pool.malloc!(int)(new_size);
		
		// copy old vector values
		for (int i=0;i<this.freeIndCount;++i) {
			this.freeInd[i] = old_freeInd[i];
		}
		
		this.freeIndSize = new_size;
	}
	
	
	public void RemoveElement(int ind) 
	{
		if (this.freeIndCount==this.freeIndSize) {
			IncreaseFreeInd();
		}
		// mark vector space as free
		this.freeInd[this.freeIndCount++] = ind;
		
		
	
		// update all the trees
		for (int t = 0; t < this.numTrees; ++t ) {
			Tree node = this.trees[t];
			
			if (node.child1==null && node.child2==null && node.divfeat==ind) {
				this.trees[t] = null;
			}
			else {
					RemoveFromTree(node,null,ind);
			}
		}
	}
	
	
	
	private void RemoveFromTree(Tree node,Tree parent, int ind)
	{
		float* vec = this.vecs[ind];
		
		if (node.child1==null && node.child2==null && node.divfeat==ind) {
			// remove element
			Tree otherChild;
			if (parent.child1==node) {
				otherChild = parent.child2;
			}
			else {
				otherChild = parent.child1;
			}
			
			// remove node from tree
			parent.divfeat = otherChild.divfeat;
			parent.divval = otherChild.divval;
			parent.child1 = otherChild.child1;
			parent.child2 = otherChild.child2;
		}
		else {
			if (vec[node.divfeat]<node.divval) {
				RemoveFromTree(node.child1,node,ind);
			} else {
				RemoveFromTree(node.child2,node,ind);
			}
		}
		
	}
	
	
	/*----------------------- Nearest Neighbor Lookup ------------------------*/
	
	
	/* Find set of numNN nearest neighbors to vec, and place their indices
		(location in original vector given to BuildIndex) in result.
	*/
	public void FindNeighbors(int *result, int numNN, float *vec, int maxCheck)
	{
		GetNeighbors(result, numNN, vec, maxCheck);
	}
	
	
	private void GetNeighbors(int *result, int numNN, float *vec, int maxCheck)
	{
		int i;
		BranchSt branch;
	
		this.ncount = 0;
		this.checkCount = 0;
		this.heap.init();
		this.checkID -= 1;  /* Set a different unique ID for each search. */
	
		/* Make sure this.dsqs is long enough to hold numNN values. */
		if (numNN > this.dsqlen) {
			this.dsqs = this.pool.malloc!(float)(numNN);
			this.dsqlen = numNN;
		}
	
		/* Search once through each tree down to root. */
		for (i = 0; i < this.numTrees; i++) {
			SearchLevel(result, numNN, vec, this.trees[i], 0.0,
					maxCheck);
		}
	
		/* Keep searching other branches from heap until finished. */
		
		
		while (((branch = this.heap.getMin()) != null) 
			&& (this.checkCount < maxCheck || this.ncount < numNN)) {
			SearchLevel(result, numNN, vec, branch.node,
					branch.mindistsq, maxCheck);
		}
		assert(this.ncount == numNN);
	}
	
	
	/* Search starting from a given node of the tree.  Based on any mismatches at
		higher levels, all exemplars below this level must have a distance of
		at least "mindistsq". 
	*/
	private void SearchLevel(int *result, int numNN, float *vec,
			Tree node, float mindistsq, int maxCheck)
	{
		float val, diff;
		Tree bestChild, otherChild;
	
		/* If this is a leaf node, then do check and return. */
		if (node.child1 == null  &&  node.child2 == null) {
			CheckNeighbor(result, numNN, node.divfeat, vec);
			return;
		}
	
		/* Which child branch should be taken first? */
		val = vec[node.divfeat];
		diff = val - node.divval;
		bestChild = (diff < 0) ? node.child1 : node.child2;
		otherChild = (diff < 0) ? node.child2 : node.child1;
	
		/* Create a branch record for the branch not taken.  Add distance
			of this feature boundary (we don't attempt to correct for any
			use of this feature in a parent node, which is unlikely to
			happen and would have only a small effect).  Don't bother
			adding more branches to heap after halfway point, as cost of
			adding exceeds their value.
		*/
		if (2 * this.checkCount < maxCheck  ||  this.ncount < numNN) {
			this.heap.insert( BranchSt(otherChild, cast(int)(mindistsq + diff * diff)) );
		}
	
		/* Call recursively to search next level down. */
		SearchLevel(result, numNN, vec, bestChild, mindistsq, maxCheck);
	}
	
	/*-------------------- Final check of each leaf node ----------------------*/
	
	/* Measure distance from vec to the vector indicated by vindex
		and add it to the result list if appropriate.
	*/
	private void CheckNeighbor(int *result, int numNN, int vindex, float *vec)
	{
		int i, ncount;
		float distsq;
		float* dsqs;
	
		this.checkCount += 1;
	
		/* Do not check same node more than once when searching multiple trees.
			Once a vector is checked, we set its location in this.vind to the
			current this.checkID.
		*/
		if (this.vind[vindex] == this.checkID)
			return;
		this.vind[vindex] = this.checkID;
	
		distsq = DistSquared(vec, this.vecs[vindex], this.veclen);
		dsqs = this.dsqs;   /* Vector of distances to current results. */
		ncount = this.ncount;
	
		/* If result list is not full, then add this to end. */
		if (ncount < numNN) {
			result[ncount] = vindex;
			dsqs[ncount++] = distsq;
			this.ncount += 1;
	
			/* Replace last element of result with this, if it is closer. */
		} else if (distsq < this.dsqs[ncount-1]) {
			result[ncount-1] = vindex;
			dsqs[ncount-1] = distsq;
	
			/* Else return, as distance is larger than existing neighbors. */
		} else
			return;
	
		/* Bubble last element of results down to correct position. */
		i = ncount - 1;
		while (i >= 1  &&  dsqs[i] < dsqs[i-1]) {
			/* Swap results in i and i-1. */
			swap(result[i],result[i-1]);
			swap(dsqs[i],dsqs[i-1]); 
			i--;
		}
	}
	
	
	/* Return the squared distance between two vectors. 
		This is highly optimized, with loop unrolling, as it is one
		of the most expensive inner loops of recognition.
	*/
	private float DistSquared(float *v1, float *v2, int veclen)
	{
		float diff, distsq = 0.0;
		float diff0, diff1, diff2, diff3;
		float *final_, finalgroup;
	
		final_ = v1 + veclen;
		finalgroup = final_ - 3;
	
		/* Process 4 pixels with each loop for efficiency. */
		while (v1 < finalgroup) {
			diff0 = v1[0] - v2[0];
			diff1 = v1[1] - v2[1];
			diff2 = v1[2] - v2[2];
			diff3 = v1[3] - v2[3];
			distsq += diff0 * diff0 + diff1 * diff1 + diff2 * diff2 + diff3 * diff3;
			v1 += 4;
			v2 += 4;
		}
		/* Process last 0-3 pixels.  Not needed for standard vector lengths. */
		while (v1 < final_) {
			diff = *v1++ - *v2++;
			distsq += diff * diff;
		}
		return distsq;
	}
	

}